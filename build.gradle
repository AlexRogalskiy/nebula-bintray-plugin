/*
 * Copyright 2014-2015 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript { repositories { jcenter() } } // Some plugin dependencies (namely JFrog) are not in Maven Central

plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'nebula.info' version '3.0.3'
    id 'nebula.contacts' version '3.0.1'
    id 'nebula.maven-publish' version '4.2.2'
    id 'nebula.nebula-release' version '3.0.5'
    id 'nebula.nebula-bintray' version '3.1.0'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'nebula.javadoc-jar' version '4.2.2'
    id 'nebula.source-jar' version '4.2.2'
    id 'nebula.maven-apache-license' version '4.2.2'
    id 'com.github.kt3k.coveralls' version '2.4.0'

    id 'nebula.dependency-lock' version '3.0.0'
    id 'org.ysb33r.gradletest' version '0.5.4'
}

apply from: 'https://raw.githubusercontent.com/nebula-plugins/nebula-core/f187498b01c752d139f33fd5b54871d04623c838/common.gradle'

description 'Additional Bintray tasks'

group = 'com.netflix.nebula'

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'quidryan'
    }
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    compile 'org.jfrog.buildinfo:build-info-extractor-gradle:3.+'
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-bintray-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-bintray-plugin.git'
    description = 'Plugins to configure common  configuration'

    plugins {
        nebulaBintray {
            id = 'nebula.nebula-bintray'
            displayName = 'Nebula Bintray plugin'
            description = 'Applies nebula.nebula-bintray-publishing and nebula.nebula-ojo-publishing'
            tags = ['nebula', 'bintray']
        }
        nebulaBintrayPublishing {
            id = 'nebula.nebula-bintray-publishing'
            displayName = 'Nebula Bintray Publishing plugin'
            description = 'Uploads candidate and final artifacts to bintray with Nebula defaults'
            tags = ['nebula', 'bintray']
        }
        nebulaOjoPublishing {
            id = 'nebula.nebula-ojo-publishing'
            displayName = 'Nebula OJO Publishing plugin'
            description = 'Uploads snapshots to oss.jfrog.org with Nebula defaults'
            tags = ['nebula', 'bintray']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'nebula-bintray-plugin'
    }
}

gradleTest {
    versions '2.2.1', '2.3', '2.4', '2.5', '2.6', '2.7-rc-1'
}

gradleTest.dependsOn(jar)
gradleTest.doFirst {
    project.version = project.version.toString()
}
