buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.netflix.nebula:nebula-plugin-plugin:3.0.5'
    }
}

plugins {
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'nebula.maven-publishing' version '3.0.2'
    id 'nebula.javadoc-jar' version '3.0.2'
    id 'nebula.source-jar' version '3.0.2'
    id 'nebula.apache-license-pom' version '3.0.2'
    id 'nebula.info' version '2.2.4'
    id 'nebula.contacts' version '2.2.0'
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'com.github.kt3k.coveralls' version '2.4.0'
    id "org.ysb33r.gradletest" version "0.5.4"
}

description 'Additional Bintray tasks'
apply plugin: 'nebula.plugin-plugin'

group = 'com.netflix.nebula'

contacts {
    'justin@halfempty.org' {
        moniker 'Justin Ryan'
        github 'quidryan'
    }
    'rob.spieldenner@gmail.com' {
        moniker 'Rob Spieldenner'
        github 'rspieldenner'
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'com.jfrog.bintray.gradle:gradle-bintray-plugin:latest.release'
    compile 'org.jfrog.buildinfo:build-info-extractor-gradle:latest.release'
    testCompile('com.netflix.nebula:nebula-test:3.0.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

pluginBundle {
    website = 'https://github.com/nebula-plugins/nebula-bintray-plugin'
    vcsUrl = 'https://github.com/nebula-plugins/nebula-bintray-plugin.git'
    description = 'Plugins to configure common  configuration'

    plugins {
        nebulaBintray {
            id = 'nebula.nebula-bintray'
            displayName = 'Nebula Bintray plugin'
            description = 'Applies nebula.nebula-bintray-publishing and nebula.nebula-ojo-publishing'
            tags = ['nebula', 'bintray']
        }
        nebulaBintrayPublishing {
            id = 'nebula.nebula-bintray-publishing'
            displayName = 'Nebula Bintray Publishing plugin'
            description = 'Uploads candidate and final artifacts to bintray with Nebula defaults'
            tags = ['nebula', 'bintray']
        }
        nebulaOjoPublishing {
            id = 'nebula.nebula-ojo-publishing'
            displayName = 'Nebula OJO Publishing plugin'
            description = 'Uploads snapshots to oss.jfrog.org with Nebula defaults'
            tags = ['nebula', 'bintray']
        }
    }

    mavenCoordinates {
        groupId = 'com.netflix.nebula'
        artifactId = 'nebula-bintray-plugin'
    }
}

bintray {
    publications = ['nebula']
}

gradleTest {
    versions '2.2.1', '2.3', '2.4', '2.5', '2.6'
}

gradleTest.dependsOn(jar)
gradleTest.doFirst {
    project.version = project.version.toString()
}
